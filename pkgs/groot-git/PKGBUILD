# Maintainer: Filippo Squillace <feel dot sqoox at gmail dot com>

pkgname=groot-git
pkgver=1.0.1
pkgrel=1
pkgdesc="Yet another enhanced chroot command with mounting capabilities"
arch=(any)
url="https://github.com/fsquillace/groot"
license=('GPL')
provides=(groot)
conflicts=(groot)
source=("git+$url.git")
md5sums=(SKIP)

pkgver() {
  cd ${pkgname%-git}

  # Git, tags available
  printf "%s" "$(git describe --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

check() {
  cd ${pkgname%-git}/tests/unit-tests
  ./test-groot.sh
}

package() {
  cd ${pkgname%-git}
  install -D     bin/groot "$pkgdir"/usr/bin/groot
  install -Dm644 LICENSE   "$pkgdir"/usr/share/licenses/${pkgname%-git}/LICENSE
}

# vim:set ts=2 sw=2 et:
